apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId "com.yazhi1992.moon"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 8
        versionName "0.0.8"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

        //bugly 升级
        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86'
        }

        manifestPlaceholders = [
                XG_ACCESS_ID : "2100298761",
                XG_ACCESS_KEY: "AB73DNT6B15K",
                PACKAGE_NAME : "com.yazhi1992.moon",
        ]
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    signingConfigs {
        release {
            storeFile file("../moon.keystore")
            storePassword "zyz123456"
            keyAlias "lina"
            keyPassword properties.getProperty("pwd")
            v2SigningEnabled true
        }
    }

    dataBinding {
        enabled true
    }

    def gitCommitCount = "git rev-list HEAD --count".execute().text.trim()

    buildTypes {
        debug {
            buildConfigField "String", "LEAN_CLOUD_ID", properties.getProperty("leanCloudId")
            buildConfigField "String", "LEAN_CLOUD_KEY", properties.getProperty("leanCloudKey")
            buildConfigField "String", "QQ_ID", "\"1106624697\""
            buildConfigField "String", "QQ_KEY", "\"IeOM0d7nzg2uIAps\""
            buildConfigField "String", "BUGLY_ID", "\"d29b66c253\""
            buildConfigField "String", "LEANCLOUD_QQ_LOGIN_URL", "\"https://leancloud.cn/1.1/sns/callback/ypt2m1s8xla33l8o\""

            minifyEnabled false // 是否进行混淆
            applicationIdSuffix '.debug'
            versionNameSuffix ".${gitCommitCount}"
        }
        release {
            buildConfigField "String", "LEAN_CLOUD_ID", properties.getProperty("leanCloudId")
            buildConfigField "String", "LEAN_CLOUD_KEY", properties.getProperty("leanCloudKey")
            buildConfigField "String", "QQ_ID", "\"1106624697\""
            buildConfigField "String", "QQ_KEY", "\"IeOM0d7nzg2uIAps\""
            buildConfigField "String", "BUGLY_ID", "\"5d768fb313\""
            buildConfigField "String", "LEANCLOUD_QQ_LOGIN_URL", "\"https://leancloud.cn/1.1/sns/callback/ypt2m1s8xla33l8o\""

            minifyEnabled false
            versionNameSuffix ".${gitCommitCount}"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    //robolectric
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        debug {
            res.srcDirs = ['src/debug/res']
        }
    }

    kapt {
        generateStubs = true
    }
}

//greendao配置
greendao {
    schemaVersion 3  //版本号
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation rootProject.ext.dependencies.constraintLayout
    implementation rootProject.ext.dependencies.supportDesign
    api rootProject.ext.dependencies.kotlin_jdk8

    api project(":yazhilib")
    api (project(":matisse")) {
        exclude group: 'com.github.bumptech.glide', module: 'glide'
    }

    //arouter
    api rootProject.ext.dependencies.arouter
    kapt rootProject.ext.dependencies.arouterCompiler

    //robolectric
    testImplementation rootProject.ext.dependencies.robolectric

    //logger
    implementation rootProject.ext.dependencies.logger

    //okhttp
    implementation rootProject.ext.dependencies.okhttp

    api(rootProject.ext.dependencies.permissionsDispatcher) {
        // if you don't use android.app.Fragment you can exclude support for them
        exclude module: "support-v13"
    }
    kapt rootProject.ext.dependencies.permissionsDispatcherProcessor

    //bugly
    api rootProject.ext.dependencies.buglyCrash
    api rootProject.ext.dependencies.buglyNativeCrash

    api rootProject.ext.dependencies.eventbus

    //LeanCould
    api rootProject.ext.dependencies.leancloudAvoscloudSdk

    //recyclerview 多布局
    implementation(rootProject.ext.dependencies.multitype, {
        exclude group: 'com.android.support'
    })

    implementation rootProject.ext.dependencies.glide
    kapt rootProject.ext.dependencies.glideCompiler

    api rootProject.ext.dependencies.multiDex

    //下拉刷新
    api rootProject.ext.dependencies.smartRefresh
    api rootProject.ext.dependencies.smartRefreshHeader

    //dagger
    api rootProject.ext.dependencies.dagger
    kapt rootProject.ext.dependencies.daggerCompiler
    api rootProject.ext.dependencies.daggerAndroid

//    api rootProject.ext.daggerAndroidSupport
//    annotationProcessor rootProject.ext.daggerAndroidProcessor

    api rootProject.ext.dependencies.greenDao
    api rootProject.ext.dependencies.greenDaoHepler

    api rootProject.ext.dependencies.luban

    //信鸽jar
    api rootProject.ext.dependencies.xinge
    //wup包
    api rootProject.ext.dependencies.xinge_wup
    //mid包
    api rootProject.ext.dependencies.xinge_mid
    //在集成信鸽的基础上新增小米push的jar包
    api rootProject.ext.dependencies.xinge_mipush

    api rootProject.ext.dependencies.lifeCycle
    kapt rootProject.ext.dependencies.lifeCycleCompiler
}

//apply from: 'tinker-support.gradle'